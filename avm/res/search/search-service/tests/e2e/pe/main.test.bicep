targetScope = 'subscription'

metadata name = 'Private endpoint-enabled deployment'
metadata description = 'This instance deploys the module with private endpoints.'

// ========== //
// Parameters //
// ========== //

@description('Optional. The name of the resource group to deploy for testing purposes.')
@maxLength(90)
param resourceGroupName string = 'dep-${namePrefix}-search.searchservices-${serviceShort}-rg'

@description('Optional. The location to deploy resources to.')
param resourceLocation string = deployment().location

@description('Optional. A short identifier for the kind of deployment. Should be kept short to not run into resource-name length-constraints.')
param serviceShort string = 'ssspe'

@description('Generated. Used as a basis for unique resource names.')
param baseTime string = utcNow('u')

@description('Optional. A token to inject into the name of each resource.')
param namePrefix string = '#_namePrefix_#'

// ============ //
// Dependencies //
// ============ //

// General resources
// =================
resource resourceGroup 'Microsoft.Resources/resourceGroups@2021-04-01' = {
  name: resourceGroupName
  location: resourceLocation
}

module nestedDependencies 'dependencies.bicep' = {
  scope: resourceGroup
  name: '${uniqueString(deployment().name, resourceLocation)}-nestedDependencies'
  params: {
    virtualNetworkName: 'dep-${namePrefix}-vnet-${serviceShort}'
    applicationSecurityGroupName: 'dep-${namePrefix}-asg-${serviceShort}'
    storageAccountName: 'dep${namePrefix}st${serviceShort}'
    keyVaultName: 'dep-${namePrefix}-kv-${serviceShort}-${substring(uniqueString(baseTime), 0, 3)}'
    privateDnsZoneName: 'privatelink.search.windows.net'
    location: resourceLocation
  }
}

// ============== //
// Test Execution //
// ============== //

module testDeployment '../../../main.bicep' = {
  scope: resourceGroup
  name: '${uniqueString(deployment().name, resourceLocation)}-test-${serviceShort}'
  params: {
    name: '${namePrefix}${serviceShort}001'
    location: resourceLocation
    publicNetworkAccess: 'Disabled'
    privateEndpoints: [
      {
        applicationSecurityGroupResourceIds: [
          nestedDependencies.outputs.applicationSecurityGroupResourceId
        ]
        privateDnsZoneGroup: {
          privateDnsZoneGroupConfigs: [
            {
              privateDnsZoneResourceId: nestedDependencies.outputs.privateDNSZoneResourceId
            }
          ]
        }
        subnetResourceId: nestedDependencies.outputs.subnetResourceId
        tags: {
          Environment: 'Non-Prod'
          Role: 'DeploymentValidation'
        }
      }
      {
        privateDnsZoneGroup: {
          privateDnsZoneGroupConfigs: [
            {
              privateDnsZoneResourceId: nestedDependencies.outputs.privateDNSZoneResourceId
            }
          ]
        }
        subnetResourceId: nestedDependencies.outputs.subnetResourceId
      }
    ]
    sharedPrivateLinkResources: [
      {
        privateLinkResourceId: nestedDependencies.outputs.storageAccountResourceId
        groupId: 'blob'
        resourceRegion: nestedDependencies.outputs.storageAccountLocation
        requestMessage: 'Please approve this request'
      }
      {
        privateLinkResourceId: nestedDependencies.outputs.keyVaultResourceId
        groupId: 'vault'
        requestMessage: 'Please approve this request'
      }
    ]
    tags: {
      'hidden-title': 'This is visible in the resource name'
      Environment: 'Non-Prod'
      Role: 'DeploymentValidation'
    }
  }
  dependsOn: [
    nestedDependencies
  ]
}
